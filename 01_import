USING PERIODIC COMMIT
LOAD CSV WITH HEADERS FROM "file:/metav/CSV/People.csv" AS row
CREATE (p:Person)
set p = row

CREATE INDEX ON :Person(PersonID)

USING PERIODIC COMMIT
LOAD CSV WITH HEADERS FROM "file:/metav/CSV/PeopleGroups.csv" AS row
MERGE (g:Group { name: row.GroupName })
ON CREATE SET g.name = row.GroupName


USING PERIODIC COMMIT
LOAD CSV WITH HEADERS FROM "file:/metav/CSV/PeopleGroups.csv" AS row
MATCH (p:Person), (g:Group) where p.PersonID = row.PersonID and g.name = row.GroupName
CREATE (p)-[:MEMBER_OF]->(g)

USING PERIODIC COMMIT
LOAD CSV WITH HEADERS FROM "file:/metav/CSV/PeopleRelationships.csv" AS row
MATCH (p1:Person), (p2:Person) where p1.PersonID = row.Primary and p2.PersonID = row.RelatedTo and row.RelType = "father"
MERGE (p2)-[:FATHER]->(p1)

USING PERIODIC COMMIT
LOAD CSV WITH HEADERS FROM "file:/metav/CSV/PeopleRelationships.csv" AS row
MATCH (p1:Person), (p2:Person) where p1.PersonID = row.Primary and p2.PersonID = row.RelatedTo and row.RelType = "mother"
MERGE (p2)-[:MOTHER]->(p1)

USING PERIODIC COMMIT
LOAD CSV WITH HEADERS FROM "file:/metav/CSV/Books.csv" AS row
CREATE (b:Book)
set b = row

CREATE INDEX ON :Book(BookID)

USING PERIODIC COMMIT
LOAD CSV WITH HEADERS FROM "file:/metav/CSV/Places.csv" AS row
CREATE (p:Place)
set p = row

CREATE INDEX ON :Place(PlaceID)

USING PERIODIC COMMIT
LOAD CSV WITH HEADERS FROM "file:/metav/CSV/Topics.csv" AS row
MERGE (t:Topic { name: row.Topic })
ON CREATE SET t.name = row.Topic

CREATE INDEX ON :Topic(name)

USING PERIODIC COMMIT
LOAD CSV WITH HEADERS FROM "file:/metav/CSV/Topics.csv" AS row
CREATE (st:Subtopic)
set st = row

CREATE INDEX ON :Subtopic(TopicID)

MATCH (t:Topic), (st:Subtopic) where t.name = st.Topic
CREATE (st)-[:SUB_TOPIC]->(t)
REMOVE st.Topic

USING PERIODIC COMMIT
LOAD CSV WITH HEADERS FROM "file:/metav/CSV/Verses.csv" AS row
CREATE (v:Verse)
set v = row

CREATE INDEX ON :Verse(VerseID)

USING PERIODIC COMMIT
LOAD CSV WITH HEADERS FROM "file:/metav/CSV/CrossRefIndex.csv" AS row
MATCH (v1:Verse), (v2:Verse) where v1.VerseID = row.VerseID and v2.VerseID = row.VerseRefID
MERGE (v1)-[:CROSS_REFERENCE]-(v2)

USING PERIODIC COMMIT
LOAD CSV WITH HEADERS FROM "file:/metav/CSV/TopicIndex.csv" AS row
MATCH (st:Subtopic), (v:Verse) where st.TopicID = row.TopicID and v.VerseID = row.VerseID
CREATE (v)-[:TOPIC]->(st)

USING PERIODIC COMMIT
LOAD CSV WITH HEADERS FROM "file:/metav/CSV/MainIndex.csv" AS row
CREATE (w:Word)
set w = row

MATCH (w:Word), (p:Person) where w.PersonID = p.PersonID
CREATE (w)-[:PERSON]->(p)
REMOVE w.PersonID

MATCH (w:Word) WHERE w.PersonID = "0"
REMOVE w.PersonID

MATCH (w:Word), (v:Verse) where w.VerseID = v.VerseID
CREATE (w)-[:VERSE]->(v)
REMOVE w.VerseID

MATCH (w:Word), (p:Place) where w.PlaceID = p.PlaceID
CREATE (w)-[:PLACE]->(p)
REMOVE w.PlaceID

MATCH (w:Word) WHERE w.PlaceID = "0"
REMOVE w.PlaceID





